apply plugin: "com.android.application"
apply plugin: "com.mlibrary.multiapk.application"
//apply plugin: "org.greenrobot.greendao"

multiApkApplication {
    solidMode = project.solidMode
    buildToolsVersion = project.buildToolsVersion
    supportLibraryVersion = project.supportLibraryVersion
    packageName = "com.multiapk"
    moduleConfigFilePath = "$rootDir/gradle/multiapk/apk_module_config.xml"
    keystore = "$rootDir/app/examples/debug.keystore"
    keyAlias = "androiddebugkey"
    keyPassword = "android"
    storePassword = "android"
//    aaptMacPath="/Volumes/android/android_source/WORKING_DIRECTORY/out/host/darwin-x86/bin/aapt"
    aaptMacPath = "/Volumes/android/android_source/WORKING_DIRECTORY/out/target/product/generic/system/bin/aapt"
//    aaptMacPath="/Users/krmao/Desktop/aapt"
    targetSdkVersion = project.targetSdkVersion
}

//noinspection GroovyMissingReturnStatement
android {

    /*greendao {
        schemaVersion 1
        targetGenDir "src/main/java"
    }*/

    compileSdkVersion project.compileSdkVersion
    buildToolsVersion = project.buildToolsVersion

    defaultConfig {
        applicationId "com.multiapk"
        versionCode 12
        versionName "12"
        minSdkVersion 18
        targetSdkVersion project.targetSdkVersion
        multiDexEnabled !solidMode
        vectorDrawables.useSupportLibrary = true
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    /*dataBinding { enabled = false }*/

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword multiApkApplication.storePassword
            keyAlias multiApkApplication.keyAlias
            keyPassword multiApkApplication.keyPassword
        }

        release {
            storeFile file("debug.keystore")
            storePassword multiApkApplication.storePassword
            keyAlias multiApkApplication.keyAlias
            keyPassword multiApkApplication.keyPassword
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            versionNameSuffix ""
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            debuggable solidMode
            jniDebuggable solidMode
            versionNameSuffix ""
            minifyEnabled !solidMode
            zipAlignEnabled !solidMode
            shrinkResources !solidMode
            proguardFiles "${rootDir}/gradle/global_proguard.pro", "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        unitTests.returnDefaultValues = false //单元测试找不到方法使用默认值开关，打开的话就看不到错误了
        resultsDir = "${buildDir}/test-reports"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        fatal 'UnusedResources'
        warning 'ResourceType' //TODO remove eventually with ButterKnife 8.0 release
    }

    configurations.all {
        resolutionStrategy {
            force "com.google.code.findbugs:jsr305:1.3.9"
            force "com.android.support:support-annotations:$supportLibraryVersion"
        }
    }
}

dependencies {
    compile fileTree(dir: "libs", include: ["*.jar"])
    compile project(":app:libraries:library-common")

    if (!solidMode) {//普通模式才需要依赖。solid模式编译时不依赖子bu。
        compile project(":app:modules:computer")
        compile project(":app:modules:mobile")
        compile project(":app:modules:mobile.android")
        compile project(":app:modules:mobile.ios")

        /******* 单元测试库 ****************************************************************************/
        // Required for local unit tests (JUnit 4 framework)
        testCompile "junit:junit:4.12"
        // http://site.mockito.org/
        // testCompile "org.mockito:mockito-core:2.+"

        // Required for instrumented tests
        androidTestCompile "com.android.support:support-annotations:$supportLibraryVersion"

        // Set this dependency to build and run UI Automator tests
        androidTestCompile "com.android.support.test.uiautomator:uiautomator-v18:2.1.2"

        // Android JUnit Runner
        androidTestCompile "com.android.support.test:runner:0.5"
        // JUnit4 Rules
        androidTestCompile "com.android.support.test:rules:0.5"
        // Espresso core
        androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
            exclude group: "com.google.code.findbugs"
            exclude group: "com.android.support", module: "support-annotations"
        })
        // Espresso-web for WebView support
        // androidTestCompile "com.android.support.test.espresso:espresso-web:2.2.2"
        // Espresso-idling-resource for synchronization with background jobs
        // androidTestCompile "com.android.support.test.espresso:espresso-idling-resource:2.2.2"
    }
}